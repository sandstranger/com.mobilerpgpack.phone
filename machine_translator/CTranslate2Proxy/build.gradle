plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
}

android {
    namespace 'com.mobilerpgpack.ctranslate2proxy'
    compileSdkVersion versions.target_sdk

    defaultConfig {
        minSdk versions.min_sdk
        targetSdk versions.target_sdk

        ndk {
            abiFilters = abi_filters
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-$versions.native_android_platform_version",
                        "-DCMAKE_CXX_FLAGS=$build_flags.CMAKE_CXXFLAGS",
                        "-DCMAKE_C_FLAGS=$build_flags.CMAKE_CFLAGS",
                        "-DCMAKE_SHARED_LINKER_FLAGS=$build_flags.CMAKE_SHADERLINKER_FLAGS",
                        "$build_flags.CMAKE_ANDROID_STL"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version versions.cmake_version
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildToolsVersion versions.build_tools_version
    ndkVersion versions.ndk_version
}

preBuild.dependsOn ':third_party:CTranslate2:build', ':third_party:sentencepiece:build'

dependencies {
    implementation(libs.icu4j)
    implementation (project(":third_party:CTranslate2"))
    implementation (project(":third_party:sentencepiece"))
    implementation(libs.jna){ artifact { type = "aar" } }
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}